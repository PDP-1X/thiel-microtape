score

/WAR with scorekeeper 22oct70

3/brk,	jmp sb
4/	jmp in

grc,	scr 2s	/gravity constant
rc,	4000	/rotation constant
acc,	600	/acceleration constant
tcr,	5000	/collision radius
clt,	-1000	/torpedo collision life
cls,	-2000	/ship collision life
ran,	253142	/random number
trt,	-1000	/torpedo recharge time
sco,	4000	/solar collision radius
hdt,	-10000	/hyperspace dormant time
hbt,	-1000	/hyperspace breakout time
hyv,	1000	/hyperspacially induced velocity
hav,	2000	/hyperspatially induced angular velocity
hun,	6	/hyperspacial uncertainty
hrt,	-10000	/hyperspace recharge time
grt,	-10000	/game reset time
tlf,	10000	/torpedo life
tve,	1600	/torpedo velocity
ds,	600	/ship size
tnu,	-50	/number of torpedos
ful,	-100000	/amount of fuel
sdtm,	-140	/score display time




define random
	jdp rnu
terminate


/gravity calculation
/also updates position
/skip unless hit sun

grv,	0
	lac i r
	spq
	jmp grw	/already in sun
	dac ‾rg
	lac i x
	mul i x
	scr 2s
	scl 1
	dac ‾r2a
	rir 1
	dio ‾r2b
	lac i y
	mul i y
	scr 2s
	scl 1
	rir 1
	adm r2a
	lac r2b
	A+II<M
	jmp .+4
	idx r2a
	lac (add-1
	A+II
	ril 1
	dio r2b
sr,	lac r2a
	div rg
	lac (add-1
	lia
	lxr rg
	I+XA
	rar 1
	and (add-1
	dac rg
	XMIX<
	CXX
	lio r2b
	SX>=
	jmp sr
	sub sco	/solar collision radius
	spa
	jmp grx	/hit sun
	ZX
	lac r2a
ne,	spa
	jmp ng
	rcl 1
	SXX
	jmp ne
ng,	rcr 1
	dac ‾fr
	lac (-132403
	cli
	div fr
	hlt
	dac ‾fr
	lio (add
	CXX|
	sir 1
	SXX>=
	jmp .-2
	law scl×1000
	rcl 9s
	dac sh1
	law scl×1000
	rcl 9s
	dac sh2
	lxr bn
	lac rg
	dac i r
	lac fr
	mul i y
	div rg
	hlt
	dac ‾fy
	lac fr
	mul i x
	div rg
	hlt
	mul dt
	scr 9s
sh1,	0
sh2,	0
	xct grc
	jdp dpa
	lac i dx
	mul dt
	scl 6s
	adm i x
	lac fy
	mul dt
	scr 9s
	xct sh1
	xct sh2
	xct grc
	SXX
	jdp dpa
	lac i dy-1
	mul dt
	scl 6s
	adm i y-1
grw,	idx grv
	jmp i grv

grx,	lxr bn
	dzm i r
	jmp i grv


/start

in,	iam
	lsm
	dra
	ril 6s
	dio ‾day
	law ste-3
	spi
	law stt [[ste-stt]>6]×3
	dap stb
	law 5004
	arq
	dsm
	tyi
	law 4
	AMI<
	sni
	jmp .-4
	clc 60
	tyo
	dac ‾nsh
	sad (4
	jmp setup4
	sad (3
	jmp setup3
setup2,	lxr (pd2
	lac (200200
	dac i 23
	dzm i 22
	jmp contin
setup3,	lxr (pd2
	lac (257720
	dac i 23
	dzm i 22
	lxr (pd3
	lac (720257
	dac i 23
	dzm i 22
	jmp contin
setup4,	lxr (pd2
	lac (200600
	dac i 23
	dzm i 22
	lxr (pd3
	lac (200200
	dac i 23
	dzm i 22
	lxr (pd4
	lac (600200
	dac i 23
	dzm i 22
contin,	dzm ‾games
	lxr (pd1
	dzm i 22
	cac
	cbs
	asc 100
	esm



/initialization

ini,	lac ds
	sal 3s
	dac ‾8ss
	lac tcr
	mul (265012	/sqrt(2)
	dac ‾tc2
	sal 1
	add tc2
	dac ‾tc32
	lac hdt
	cma
	sar 1
	dac ‾hhd
	add hbt
	cma
	dac ‾hbd
	lac (lac
	mul (1
	div nsh
	hlt
	dac fy
	dzm sdc
	dzm stc
	dzm dtc
	lac nsh
	dac ‾ships
	dac fx
	add (2
	cma
	dac ‾points
	lac nsh
	mul (l
	law tor
	AMIA
	dac ‾bes	/beginning of object storage
	dac ‾bn
	law l2
	mul nsh
	rir 1
	law pd1 l2
	AMIAX
	dac ‾snp	/address of first private block
	dac ‾sn
inb,	TAX
	law i 1
	adm fx
	mul fy
	scl 1s
	CIA
	add (500000
	dac rt	/angle from sun
	jdp sin
	mul (265012
	dac i 13
	lac rt
	add (lac
	jdp sin
	mul (265012
	dac i 12
	lac rt
	add (540000
	dac i 11
	dzm i 1
	dzm i 7
	dzm i hypr
	lac (skp i
	dac i 0
	dac i 2
	dac i 14
	dzm i hub
	lac tnu
	dac i 5
	lac ful
	dac i 6
	lac nsh
	SAA
	adm i 22
	lxr bn
	lio fx
	rir 3s
	dio i x	/very kludgy
	dio i y
	dio i x+l
	dio i y+l
	dzm i dx
	dzm i dx+l
	dzm i dy
	dzm i dy+l
	dzm i dx+2
	dzm i dx+2+l
	dzm i dy+2
	dzm i dy+2+l
	law 7777
	dac i r
	dac i r+l
	dac i 7
	dac i 7 l
	law l×2
	adm bn
	law l2
	adm sn
	sas (pd1 l2
	jmp inb
	law tor
	dap ‾eot
	lio (jmp b34
	law 3
	sad nsh
	lio (jmp b3
	dio pd3+20


/main loop (gravity, buttons, and display)

mvs,	dzm ‾en	/total ships
	dzm ‾et	/total torpedoes
	nop 4
	lsm
	lac dtc
	dzm dtc
	esm
	dac ‾dt
	sar 6s
	dac ‾dts
	lac rc
	mul dt
	scl 7s
	dac ‾rcf
	lac acc
	mul dt
	dac ‾acf	/dt-fixed acceleration constant
	lac bes
	dac ‾bn
	lac snp
	dac ‾sn
mvn,	TAX
	xct i 14
	jmp nxs	/doesn't exist
	idx en
	lac i 5
	adm et
	cla
	dap acs
	lac ds
	dac ‾dds
	xct i 0
	jmp hyt	/in hyperspace
	lxr bn
	lio i y
	lxr i x
	law l
	add bn
	X→AX
	jdp col
	skp i
	law l
	A+XX
	lac i x
	lio i y
	jdp col
	skp i
	lxr sn
	lac i 12	/restore ship x and y
	lio i 13
	lxr bn
	dac i x
	dio i y
rhp,	jdp grv	/calculate gravity
	jmp hit	/fell into sun
	lxr sn
	xct i 2
	jmp sxp	/exploding
	lac i hypr
	spa
	add dts
	dac i hypr
	lac i 7
	spa
	add dts
	dac i 7
ckr,	xct i 20	/get buttons
b1,	rbt 100
br,	lac rcf	/dt-fixed rotation constant
	szs 10
	sar 6s	/7s?
	spi i
	cma
	ril 1
	sma∨spi i
	jmp bud
	lac i hypr	/hyperspace
	spa
	jmp br1	/not yet
	xct i 0
	jmp br1	/already in
	law 600
	dap i 0
	lac hdt
	dac i 15
	lxr bn
	dzm i 7
	dzm i 7+l
	jmp nxs



bud,	A~I<M
	adm i 1
br1,	lac i 1
	adm i 11
	szs i 10
	dzm i 1
	dio ‾btt
	jdp sin
	dac ‾sine
	lac (lac
	add i 11
	jdp sin
	dac ‾cosine
	xct i 0
	jmp dsh
	lio btt
	ril 1
	lac i 6
	spa∨spi i
	jmp bun
	add dts	/accelerating
	dac i 6
	law 600
	dap acs
	lxr bn
	lac acf
	mul cosine
	jdp dpa
	SXX
	lac acf
	mul sine
	jdp dpa
	lxr sn
	lio btt
	ril 1
bun,	ril 1
	lac i 7
	sma∨spi i
	jmp dsh
	lac i 5
	sma
	jmp dsh	/all gone
	idx i 5	/fire torpedo
	lac trt	/initial recharge time
	dac i 7
	lxr bn
	lac i x
	lio i y
	lxr eot
	dac i x
	dio i y
	lxr bn
	lac i dx
	lio i dy
	lxr eot
	dac i dx
	dio i dy
	lac rg	/?
	dac i 6
	lac tlf
	dac i 7	/life
	lac tc32	/gives torpedo lead on ship
	mul cosine
	adm i x
	lac tc32
	mul sine
	adm i y
	lac tve
	mul cosine
	adm i dx
	lac tve
	mul sine
	adm i dy
	law l
	adm eot
	lxr sn


/orient and display ship

dsh,	lac i 17
	dap osk
	lac sine
	mul dds
	dac rt 0
	dac rt 10
	CAAX
	dac rt 4
	lac cosine
	mul dds
	dac rt 2
	dac rt 12
	cma
	dac rt 6
	X+AI
	dio rt 5
	cmi
	dio rt 1
	dio rt 11
	XMAI
	dio rt 3
	cmi 2
	dio rt 7
	law rt 0
	dap osy
	law rt 2
	dap osx

osk,	law
	dap osm
	lxr bn
	lio i x
	lac i y

osm,	lxr
	TXX>P
	jmp osl
	skp 2
	CXX
osy,	add i
	swp
osx,	add i
	iot 7
	TAX
	idx osm
	X→IA
	jmp osm

osl,	skp 2
	jmp osn
	law rt 10
	dap osy
	law rt 12
	dap osx
	stf 2
	jmp osk

osn,	lxr sn
	xct i 0
	jmp nxs
	lxr bn
acs,	skp	/skip if accelerating
	jmp osp
	law i 7777	/ship is accelerating
	ior ran
	sub 8ss	/8 times size parameter
	dac ‾foo
	mul cosine
	dac ‾fx
	lac foo
	mul sine
	lio fx
	add i y
	swp
	add i x
	iot 7	/display exhaust
osp,	lac tc2
	mul sine
	dac fx
	lac tc2
	mul cosine
	dac fy
scp,	lac i x	/save x and y
	lio i y
	lxr sn
	dac i 12
	dio i 13
/set up ship collision points
	lxr bn
	add fx
	dac i x
	sub fx
	sub fx
	dac i x+l
	lai
	add fy
	dac i y
	sub fy
	sub fy
	dac i y+l
	lac i r
	dac i r+l
nxs,	law l×2
	adm bn
	law l2
	adm sn
	sas (pd1 l2
	jmp mvn


/sun and star display

	lac sdc
	spq
	jmp dyt	/not yet
	law i 1
	adm sdc
stb,	law .
	dac ‾stn
stx,	lxr stn
	lac i 2	/brightness
	dap dpi
	lac i 0	/x
	add day
	sma
	jmp stm
	add (100000
	spa
	jmp sdp
	sub (40000
	ral 3s
	lio i 1
dpi,	iot
	law i 3
	add stn
	sad (stt-3
	law ste-3
	dac stn
	jmp stx

stm,	law i 3
	add stb
	sad (law stt-3
	lac (law ste-3
	dac stb
	jmp stb

sdp,	random
	rcr 9s
	and (340
	spi
	cma
	dac fx
	rcl 9s
	and (340
	dac fy
	law 17
	and ran
	CAX
	ZAI
sds,	add fx
	swp
	add fy
	swp
	iot 7
	cma∨cmi
	iot 7
	cma∨cmi
	SXX>
	jmp sds

/move and display torpedos

dyt,	law tor
dyu,	sad eot
	jmp eog
	TAX
	lac i x
	lio i y
	jdp col
	nop
	jdp grv
	jmp tex-1	/fell into sun
	lxr bn
	lac i 7
	spq
	jmp tex	/exploding
	sub dts
	dac i 7
	TA>P
	jmp tex+1	/died
	lac i x
	lio i y
	iot 107
tdm,	law l
	adm bn
	jmp dyu

tex-1,	lac clt
tex,	add dts
tex+1,	dac i 7
	lac (sar 2s	/radius of torpedo explosion
	jdp exp
	lac i 7
	spa
	jmp tdm
	law i l	/torpedo must be removed from list
	adm eot
	XMAA<
	jmp eog
	cma
	dap dyl
	cla
	dap dym
dyl,	lac i
dym,	dac i
	idx dyl
	idx dym
	sas (dac i l
	jmp dyl
	TXXA
dye,	jmp dyu


/end of game check

eog,	lio en
	law 1
	AMI<
	jmp end
	lac et
	TA=
	jmp mvs
end,	lac ‾fc
	sma
	lac grt
	add dts
	dac fc
	spa
	jmp mvs


score,	lxr snp
	lac sdtm
	dac ‾sdtime
	idx games
scorn,	lio i 23
	lac i 22
	jdp sdpt
	law l2
	X+AAX
	sas (pd1 l2
	jmp scorn
	lac games
	cli
	jdp sdpt
	sdl
	isp sdtime
	jmp . 2
scorx,	jmp ini
	lxr snp
	jmp scorn

subpts,	0
	dac ‾accum
	xct i 2
	jmp i subpts
	law i 1
	adm ships
	szf 4
	jmp togthr
	spq

	jmp . 3
	idx points
	jmp . 4
	law i 1
	jmp . 2
togthr,	lac points
	adm i 22
	nop 14
	lac accum
	jmp i subpts




/hyperspace processing

hyt,	lac i 15
	add hhd
	lia
	lac dts
	adm i 15
	sma
	jmp hyo	/coming out
	add hhd
	A~I>P
	jmp hyr
	sma
	cma
	sub hbd
	sma	/div?
	jmp nxs
	mul ds
	div hbt
	hlt
	dac dds
	jmp ckr

hyr,	random	/halfway out
	dac i 11
	random
	mul hav
	dac i 1
	random
	and (1777
	adm i 15	/adjust dormant time
	random
	mul hyv
	lxr bn
	dac i dx
	random
	mul hyv
	dac i dy
	random
	dac i x
	random
	dac i y
	jmp nxs

hyo,	cla
	dap i 0
	random
	spa
	cma
	mul (1
	div hun
	hlt
	law 7777
	swp
	sub i hub
	lxr bn
	dio i r
	spq
	jmp hit	/too bad
	law 1	/made it
	dac i 7
	lxr sn
	idx i hub
	lac hrt
	dac i hypr
	lxr bn
	jmp rhp

/ship explosions

hit,	lxr bn	/exploded
	lac cls
	dac i 7
	lxr sn
	jdp subpts
	law 600
	dap i 2
sxp,	lxr bn
	lac dts
	adm i 7
	spa
	jmp spx
	dzm i 7	/dead
	dzm i 7+l
	law 600
	lxr sn
	dap i 14
	lxr bn
spx,	lac (skp i
	jdp exp
	dzm fx
	dzm fy
	jmp scp


/collision testing
/jdp col
/followed by skp if ship, nop if torpedo

define avg x
	sub x
	sar 1
	adm x
terminate

col,	0
	dac ‾fx
	dio ‾fy
coo,	law l
	A+XAX
	sad eot
	jmp cox
	lac i x
	sub fx
	div tcr
	jmp coo
	lac i y
	sub fy
	div tcr
	jmp coo
	lac i 7
	sza i
	jmp coo	/ship in hyperspace
	TXXI
	dio ‾bar
	aam
	xct col
	jmp toc
	law tor
	XMAA<M
	jmp soc
	sub (l
	mul (1	/ship-ship collision
	div (l×2	/find private block
	hlt
	TAAX
	mul (l×2
	scr 1
	law tor
	A+IA
	dac ‾foo
	X→AX
	dzm i 7
	dzm i 7+l
	mul (l2
	scr 1
	law pd1+l2
	A+IX
	law 600
	dap i 14	/goodbye
	jdp subpts
	lac (200000	/ship mass
	dac ‾tmc
	dac ‾tms
	lac i 12	/average coordinates
	lio i 13
sot,	lxr sn
	avg i 12
	dac fx
	lai
	avg i 13
	dac fy
	lxr foo	/average velocities
	lac i dx
	lio i dy
	lxr bn
	dio ‾t
	mul tmc
	scl 1
	add i dx
	scr 2s
	div tms
	hlt
	dac i dx
	lac t
	mul tmc
	scl 1
	add i dy
	scr 2s
	div tms
	hlt
	dac i dy
	lac fx
	dac i x
	dac i x+l
	lac fy
	dac i y
	dac i y+l
	lac cls
	dac i 7
	dac i 7+l
	lxr sn
	jdp subpts
	law 600
	dap i 2
tob,	lxr bar
	jmp coo

toc,	dac foo	/torpedo-torpedo collision
	lac i x
	avg fx
	lac i y
	avg fy
	law i 0	/shorten it's life
	dac i 7
	lac i dx
	lio i dy
	lxr bn
	avg i dx
	lai
	avg i dy
	lac i 7
	sma
	lac clt
	lio foo
	spi i
	lio clt
	AMI<
	lai
	dac i 7
	lac fx
	dac i x
	lac fy
	dac i y
	jmp tob

soc,	dio foo	/ship-torpedo collision
	law i 0
	dac i 7	/shorten it's life
	lac (40000	/torpedo mass
	dac tmc
	lac (240000
	dac tms
	lac i x
	lio i y
	jmp sot

cox,	idx col
	lxr bn
	jmp i col

/sequence breaks

sb,	law 200
	adm dtc
	iot 610
	isp stc
	jmp sbd
	law i 24
	dac stc
	idx sdc
	idx day
sbd,	lac 0
	jmp i 1
dtc,	0
stc,	0
sdc,	0

/double-precision add

dpa,	0
	scr 1
	scl 1
	rir 1
	adm i 2
	lac i 4
	A+IA
	dac i 4
	sma
	jmp i dpa
	and (add-1
	dac i 4
	law 1
	spi
	law i 1
	adm i 2
	jmp i dpa



/explosions

exp,	0
	dac exc	/size
	add (1000
	dac exc 1
	law i 14	/number of points
	dac foo
exb,	lio ran	/random number
	law 177
	A∧IA
	ior (scl
	dac exa
	rir 1
	lac (256051
	A~II
	A+IA
	dac ran
	scr 9s
	sir 9s
exa,	0
exc,	0
	0
	add i 1	/y
	swp
	add i 0	/x
	iot 307
	isp foo
	jmp exb
	jmp i exp


/random numbers

rnu,	0
	lio ran
	lac (514263
	A~II
	A+IA
	rcl 7s
	dac ran
	jmp i rnu



/button routines

b2,	rbt 100
	ril 5s
	jmp br
b3,	rbt 400
	rcl 6s
	ril 1s
	rcr 2s
	ril 1s
	rcr 2s
	ril 1s
	rcr 1s
	jmp br
b34,	rbt 400
	ril 9s
	jmp br
b4,	rbt 400
	rir 4s
	jmp br


/scope decimal print
/jdp sdpt
/IO contains scope coordinates
/	x, low 9 bits	y, high 9 bits
/AC number to be displayed

size_=2	/power of 2

sdpt,	0
	dio ‾sdz
sda,	cli∨swp
	scl 1
	div . 1
	12
	dac ‾sdy
	swp
	sal 1
	add (sdt
	dap sdq
	dzm ‾sdx
	law i 21
sdb,	dac ‾sdw
	dac ‾sdv
sdq,	lio .
	idx sdq
sdd,	lac sdx
	sub (400000+6007×size
	and (400000+7007×size
	spa
	add (400000+1001×size-1
	dac sdx
	ril 1
	spi i
	jmp sde
 	dio ‾sdu
	add sdz
	lia
	rar 9s
	iot 7
	lio sdu
sde,	isp sdw
	jmp sdd
	law i 22
	sas sdv
	jmp sdb
	law i 6×size
	add sdz
	and (-1000
	dac sdz
	lac sdy
	sza
	jmp sda
	jmp i sdpt

sdt,	175014	30137	/0
	27	360000	/1
	305215	31143	/2
	105014	31133	/3
	36100	217704	/4
	137114	631130	/5
	175114	231131	/6
	3610	620501	/7
	155114	231133	/8
	15114	231137	/9




/sine routine

sin,	0
	lia
	add six
	ral 4s
	and sic 3
	add sic 2
	dap sob
	lai
sob,	jmp
sic,	jdp sow
	jmp i sin
	sic
	14
	sub (lac
	jdp cow
	jmp i sin
six,	xct
	sub (add
	jdp sow
	cma
	jmp i sin
	sub (jmp
	jdp cow
	jmp .-4
sow,	0
	dac ‾sst
	mul sst
	dac cow
	sar 2s
	sub (205044
	mul (237010
	mul cow
	scl 2s
	add (311040
	mul sst
	scl 2s
	jmp i sow
cow,	0
	dac sst
	mul sst
	scl 3s
	dac sow
	sar 4s
	sub (240573
	mul (373257
	mul sow
	add (377777
	jmp i cow


/stars  3 my 1967  acg

decimal

define	mark b,x,y
x×32
y×512
b×64 7
terminate

stt,	mark 0,    1, -143	/ 33 Pisc
	mark 1,   46,  333	/ 88 Pegs, Algenib
	mark 0,   54,  447	/ 89 Pegs
	mark 0,   54, -443	/  7 Ceti
	mark 0,   82, -214	/  8 Ceti
	mark 0,  223, -254	/ 17 Ceti
	mark 0,  248,  160	/ 63 Pisc
	mark 0,  273,  -38	/ 20 Ceti
	mark 0,  329,  167	/ 71 Pisc
	mark 1,  362, -244	/ 31 Ceti
	mark 0,  376,  467	/ 84 Pisc
	mark 0,  450, -198	/ 45 Ceti
	mark 1,  490,  338	/ 99 Pisc
	mark 0,  548,  113	/106 Pisc
	mark 1,  566, -375	/ 52 Ceti
	mark 0,  570,  197	/110 Pisc
	mark 0,  595, -255	/ 53 Ceti
	mark 0,  606, -247	/ 55 Ceti
	mark 0,  615,  428	/  5 Arie
	mark 0,  617,   61	/ 14 Pisc
	mark 1,  621,  462	/  6 Arie
	mark 0,  656, -491	/ 59 Ceti
	mark 0,  665,   52	/113 Pisc
	mark 0,  727,  191	/ 65 Ceti
	mark 1,  764,  -78	/ 68 Ceti, Mira
	mark 0,  803, -290	/ 72 Ceti
	mark 0,  813,  182	/ 73 Ceti
	mark 0,  838, -357	/ 76 Ceti
	mark 0,  878,   -2	/ 82 Ceti
	mark 1,  900,   64	/ 86 Ceti
	mark 0,  907, -340	/ 89 Ceti
	mark 0,  908,  221	/ 87 Ceti
	mark 0,  913, -432	/  1 Erid
	mark 0,  947, -487	/  2 Erid
	mark 0,  976, -212	/  3 Erid
	mark 0,  992,  194	/ 91 Ceti
	mark 1, 1007,   84	/ 92 Ceti
	mark 0, 1058,  440	/ 57 Arie
	mark 0, 1076,  470	/ 58 Arie
	mark 0, 1087, -209	/ 13 Erid
	mark 0, 1104,   68	/ 96 Ceti
	mark 0, 1110, -503	/ 16 Erid
	mark 0, 1135,  198	/  1 Taur
	mark 0, 1148,  214	/  2 Taur
	mark 0, 1168,  287	/  5 Taur
	mark 0, 1170, -123	/ 17 Erid
	mark 0, 1185, -223	/ 18 Erid
	mark 0, 1191, -500	/ 19 Erid
	mark 0, 1205,    2	/ 10 Taur
	mark 1, 1243, -230	/ 23 Erid
	mark 0, 1260, -283	/ 26 Erid
	mark 0, 1304,  -74	/ 32 Erid
	mark 1, 1328, -314	/ 34 Erid
	mark 0, 1338,  278	/ 35 Taur
	mark 0, 1353,  130	/ 38 Taur 
	mark 0, 1358,  497	/ 37 Taur
	mark 0, 1405, -162	/ 38 Erid
	mark 0, 1414,  205	/ 47 Taur
	mark 0, 1423,  197	/ 49 Taur
	mark 0, 1426, -178	/ 40 Erid
	mark 0, 1430,  463	/ 50 Taur
	mark 0, 1446,  350	/ 54 Taur
	mark 0, 1463,  394	/ 61 Taur
	mark 0, 1470,  392	/ 64 Taur
	mark 0, 1476,  502	/ 65 Taur
	mark 0, 1477,  403	/ 68 Taur
	mark 0, 1483,  350	/ 71 Taur
	mark 0, 1485,  330	/ 73 Taur
	mark 0, 1495,  358	/ 77 Taur
	mark 1, 1495,  432	/ 74 Taur
	mark 1, 1496,  356	/ 78 Taur
	mark 0, 1507,  364	/
	mark 0, 1518,   -6	/ 45 Erid
	mark 0, 1526,  333	/ 86 Taur
	mark 0, 1537,  226	/ 88 Taur
	mark 3, 1537,  371	/ 87 Taur, Aldebaran
	mark 0, 1544,  -81	/ 48 Erid
	mark 0, 1551,  280	/ 90 Taur
	mark 0, 1556,  358	/ 92 Taur
	mark 0, 1557, -330	/ 53 Erid
	mark 0, 1571, -452	/ 54 Erid
	mark 0, 1596,  -78	/ 57 Erid
	mark 1, 1618,  154	/  1 Orio
	mark 0, 1622,  199	/  2 Orio
	mark 0, 1626,  124	/  3 Orio
	mark 0, 1638, -128	/ 61 Erid
	mark 1, 1644,   52	/  8 Orio
	mark 0, 1646,  228	/  7 Orio
	mark 0, 1654,  304	/  9 Orio
	mark 0, 1669,   36	/ 10 Orio
	mark 0, 1680, -289	/ 64 Erid
	mark 0, 1687, -167	/ 65 Erid
	mark 0, 1690, -460	/
	mark 0, 1690,  488	/102 Taur
	mark 0, 1700,  347	/ 11 Orio
	mark 1, 1723, -119	/ 67 Erid
	mark 0, 1729,  352	/ 15 Orio
	mark 0, 1732, -202	/ 69 Erid
	mark 0, 1750, -273	/  3 Leps
	mark 0, 1753,   63	/ 17 Orio
	mark 1, 1755, -371	/  5 Leps
	mark 0, 1756, -297	/  4 Leps
	mark 3, 1762, -189	/ 19 Orio, Rigel
	mark 1, 1779, -158	/ 20 Orio
	mark 0, 1792, -302	/  6 Leps
	mark 0, 1799, -486	/
	mark 0, 1801,  -11	/ 22 Orio
	mark 0, 1807,   79	/ 23 Orio
	mark 0, 1816, -180	/ 29 Orio
	mark 1, 1817,  -57	/ 28 Orio
	mark 0, 1818,   40	/ 25 Orio
	mark 2, 1819,  143	/ 24 Orio, Bellatrix
	mark 0, 1830,   69	/ 30 Orio
	mark 0, 1830,  497	/114 Taur
	mark 1, 1843, -474	/  9 Leps
	mark 0, 1851,  134	/ 32 Orio
	mark 0, 1857,  421	/119 Taur
	mark 1, 1860,   -8	/ 34 Orio
	mark 0, 1861, -168	/ 36 Orio
	mark 1, 1868, -407	/ 11 Leps
	mark 0, 1874,  214	/ 37 Orio
	mark 1, 1875,  225	/ 39 Orio
	mark 0, 1878, -138	/
	mark 1, 1880, -136	/ 44 Orio
	mark 0, 1880, -112	/ 42 Orio
	mark 2, 1884,  -29	/ 46 Orio
	mark 0, 1885,  210	/ 40 Orio
	mark 1, 1887,  480	/123 Taur
	mark 0, 1899,  -60	/ 48 Orio
	mark 0, 1900, -165	/ 49 Orio
	mark 0, 1900,   93	/ 47 Orio
	mark 0, 1909,  375	/126 Taur
	mark 2, 1910,  -46	/ 50 Orio
	mark 0, 1936, -511	/ 13 Leps
	mark 1, 1948, -338	/ 14 Leps
	mark 2, 1951, -221	/ 53 Orio
	mark 0, 1957,  287	/134 Taur
	mark 0, 1974, -475	/ 15 Leps
	mark 0, 1982,  461	/ 54 Orio
	mark 3, 1990,  168	/ 58 Orio, Betelgeuze
	mark 0, 2002, -323	/ 16 Leps
	mark 0, 2020,  -70	/
	mark 0, 2030,  220	/ 61 Orio
	mark 0, 2032, -241	/  3 Mono
	mark 0, 2037,  458	/ 62 Orio
	mark 0, 2057, -340	/ 18 Leps
	mark 0, 2059,  336	/ 67 Orio
	mark 0, 2084,  324	/ 70 Orio
	mark 0, 2084,  368	/ 69 Orio
	mark 0, 2105, -142	/  5 Mono
	mark 0, 2112, -311	/
	mark 2, 2152, -407	/  2 CMaj
	mark 0, 2153,  106	/  8 Mono
	mark 0, 2179, -107	/ 10 Mono
	mark 0, 2179,  462	/ 18 Gemi
	mark 0, 2184, -159	/ 11 Mono
	mark 0, 2204,  168	/ 13 Mono
	mark 2, 2230,  375	/ 24 Gemi
	mark 0, 2232, -436		/  7 CMaj
	mark 0, 2239, -413	/  8 CMaj
	mark 0, 2245, -320	/
	mark 0, 2250,  227	/ 15 Mono
	mark 0, 2266,  303	/ 30 Gemi
	mark 1, 2274,  296	/ 31 Gemi
	mark 3, 2280, -377	/  9 CMaj, Sirius
	mark 0, 2291,   57	/ 18 Mono
	mark 0, 2327,  303	/ 38 Gemi
	mark 0, 2328, -457	/ 15 CMaj
	mark 0, 2330, -271	/ 14 CMaj
	mark 0, 2340, -456	/ 19 CMaj
	mark 0, 2342, -385	/ 20 CMaj
	mark 0, 2378,  -93	/ 19 Mono
	mark 0, 2379,  471	/ 43 Gemi
	mark 0, 2385, -352	/ 23 CMaj
	mark 0, 2428,   -8	/ 22 Mono
	mark 1, 2460,  380	/ 54 Gemi
	mark 1, 2470,  504	/ 55 Gemi
	mark 0, 2491, -429	/
	mark 1, 2513,  193	/  3 CMin
	mark 0, 2519,  208	/  4 CMin
	mark 0, 2527,  278	/  6 CMin
	mark 0, 2559, -503	/
	mark 3, 2583,  125	/ 10 CMin,Procyon
	mark 0, 2597, -212	/ 26 Mono
	mark 0, 2704, -412	/
	mark 0, 2709,  -25	/ 28 Mono
	mark 0, 2714,   60	/
	mark 0, 2751,  -61	/ 29 Mono
	mark 0, 2757, -431	/ 16 Pupp
	mark 0, 2768, -288	/ 19 Pupp
	mark 0, 2794,  216	/ 17 Canc
	mark 0, 2848,  -82	/
	mark 0, 2915,  138	/  4 Hyda
	mark 0, 2921,   84	/  5 Hyda
	mark 0, 2942, -355	/  9 Hyda
	mark 0, 2944,  497	/ 43 Canc
	mark 0, 2947,   85	/  7 Hyda
	mark 0, 2951, -156	/
	mark 0, 2953,  421	/ 47 Canc
	mark 1, 2967,  154	/ 11 Hyda
	mark 0, 2968, -300	/ 12 Hyda
	mark 0, 2976,  141	/ 13 Hyda
	mark 1, 3016,  144	/ 16 Hyda
	mark 0, 3032,  279	/ 65 Canc
	mark 0, 3124,   62	/ 22 Hyda
	mark 0, 3157, -263	/ 26 Hyda
	mark 0, 3161, -208	/ 27 Hyda
	mark 2, 3201, -187	/ 30 Hyda, Alphard
	mark 0, 3209,  -53	/ 31 Hyda
	mark 0, 3225,  -17	/ 32 Hyda
	mark 0, 3261,  116	/
	mark 0, 3270,  -16	/ 35 Hyda
	mark 0, 3274, -316	/ 38 Hyda
	mark 0, 3276,  236	/ 14 Leon
	mark 0, 3338, -327	/ 39 Hyda
	mark 0, 3385,  194	/ 29 Leon
	mark 0, 3415, -286	/ 40 Hyda
	mark 1, 3424,  393	/ 30 Leon
	mark 0, 3428,  239	/ 31 Leon
	mark 0, 3429,    3	/ 15 Sext
	mark 3, 3431,  283	/ 32 Leon, Regulus
	mark 0, 3446, -270	/ 41 Hyda
	mark 0, 3495,  455	/ 40 Leon
	mark 1, 3496,  463	/ 41 Leon, Algieba
	mark 0, 3534, -372	/ 42 Hyda
	mark 0, 3557,   -3	/ 30 Sext
	mark 0, 3570,  223	/ 47 Leon
	mark 1, 3668, -357	/ nu Hyda
	mark 0, 3726, -404	/ al Crat
	mark 0, 3736,  -44	/ 61 Leon
	mark 0, 3738,  471	/ 60 Leon
	mark 0, 3754,  179	/ 63 Leon
	mark 0, 3793, -507	/ 11 Crat
	mark 1, 3805,  479	/ 68 Leon
	mark 1, 3806,  364	/ 10 Leon
	mark 0, 3821,  -71	/ 74 Leon
	mark 0, 3836, -324	/ 12 Crat
	mark 0, 3846,  150	/ 77 Leon
	mark 0, 3861,  252	/ 78 Leon
	mark 0, 3868, -390	/ 15 Crat
	mark 0, 3935, -211	/ 21 Crat
	mark 0, 3936,   -6	/ 91 Leon
	mark 0, 3981, -405	/ 27 Crat
	mark 0, 3986,  161	/  3 Virg
	mark 0, 3998,  473	/ 93 Leon
	mark 2, 4005,  344	/ 94 Leon, Denebola
	mark 0, 4013,   53	/  5 Virg
	mark 0, 4072,  163	/  8 Virg
	mark 0, 4097,  211	/  9 Virg
	mark 1, 4124, -502	/  2 Corv
	mark 1, 4157, -387	/  4 Corv
	mark 0, 4180,   -3	/ 15 Virg
	mark 0, 4185,  418	/ 11 Coma
	mark 1, 4236, -363	/  7 Corv
	mark 0, 4249, -356	/  8 Corv
	mark 0, 4290, -170	/ 26 Virg
	mark 1, 4304,  -21	/ 29 Virg
	mark 0, 4305,  245	/ 30 Virg
	mark 0, 4376, -205	/ 40 Virg
	mark 1, 4384,   90	/ 43 Virg
	mark 0, 4403,  409	/ 36 Coma
	mark 1, 4421,  262	/ 47 Virg
	mark 0, 4465, -404	/ 61 Virg
	mark 0, 4466,  411	/ 42 Coma
	mark 0, 4512, -404	/ 61 Virg
	mark 3, 4551, -242	/ 67 Virg, Spica
	mark 0, 4563, -352	/ 69 Virg
	mark 0, 4590, -131	/ 74 Virg
	mark 0, 4603,   95	/ 78 Virg
	mark 1, 4606,   -2	/ 79 Virg
	mark 0, 4679,  409	/  4 Boot
	mark 0, 4691,  371	/  5 Boot
	mark 1, 4721,  430	/  8 Boot
	mark 0, 4759,   46	/ 93 Virg
	mark 0, 4820,   66	/
	mark 0, 4822, -223	/ 98 Virg
	mark 0, 4840, -126	/ 99 Virg
	mark 3, 4842,  448	/ 16 Boot, Arcturus
	mark 0, 4857, -294	/100 Virg
	mark 0, 4864,  382	/ 20 Boot
	mark 0, 4910,  -41	/105 Virg
	mark 0, 4984,  383	/ 29 Boot
	mark 0, 4986,  322	/ 30 Boot
	mark 0, 4994, -119	/107 Virg
	mark 0, 5009,  396	/ 35 Boot
	mark 0, 5013,   53	/109 Virg
	mark 1, 5037, -356	/  9 Libr
	mark 0, 5045,  444	/ 37 Boot
	mark 0, 5074,  -90	/ 16 Libr
	mark 0, 5108,   57	/110 Virg
	mark 0, 5157, -442	/ 24 Libr
	mark 1, 5186, -205	/ 27 Libr
	mark 0, 5283, -221	/ 37 Libr
	mark 0, 5290, -329	/ 38 Libr
	mark 0, 5291,  247	/ 13 Serp
	mark 0, 5326, -440	/ 43 Libr
	mark 0, 5331,  455	/ 21 Serp
	mark 1, 5344,  153	/ 24 Serp
	mark 0, 5357,  175	/ 27 Serp
	mark 1, 5357,  358	/ 28 Serp
	mark 0, 5372,  420	/ 35 Serp
	mark 1, 5373,  -71	/ 32 Serp
	mark 0, 5381,  109	/ 37 Serp
	mark 0, 5387,  484	/ 38 Serp
	mark 0, 5394, -374	/ 46 Libr
	mark 0, 5415,  364	/ 41 Serp
	mark 0, 5419, -318	/ 48 Libr
	mark 1, 5430, -508	/  7 Scor
	mark 0, 5455, -253	/ xi Scor
	mark 1, 5459, -445	/  8 Scor
	mark 0, 5467, -464	/  9 Scor
	mark 0, 5470, -469	/ 10 Scor
	mark 0, 5497, -437	/ 14 Scor
	mark 0, 5499, -223	/ 15 Sr
	mark 1, 5513,  -78	/  1 Ophi
	mark 1, 5536, -101	/  2 Ophi
	mark 0, 5558,   29	/ 50 Serp
	mark 0, 5561,  441	/ 20 Herc
	mark 0, 5565, -451	/  4 Ophi
	mark 0, 5580,  325	/ 24 Herc
	mark 0, 5582, -415	/  7 Ophi
	mark 0, 5589, -186	/  3 Ophi
	mark 0, 5606, -373	/  8 Ophi
	mark 0, 5609,   50	/ 10 Ophi
	mark 1, 5609,  494	/ 27 Herc
	mark 0, 5610, -484	/  9 Ophi
	mark 0, 5620,  266	/ 29 Herc
	mark 1, 5641, -236	/ 13 Ophi
	mark 0, 5713, -241	/ 20 Ophi
	mark 0, 5742,  235	/ 25 Ophi
	mark 0, 5763,  217	/ 27 Ophi
	mark 0, 5807,  293	/ 60 Herc
	mark 1, 5828, -355	/ 35 Ophi
	mark 1, 5860,  330	/ 64 Herc
	mark 0, 5868,   -8	/ 41 Ophi
	mark 0, 5888, -478	/ 40 Ophi
	mark 0, 5889, -290	/ 53 Serp
	mark 0, 5924, -114	/
	mark 0, 5925,   96	/ 49 Ophi
	mark 2, 5975,  288	/ 55 Ophi
	mark 1, 5984, -349	/ 55 Serp
	mark 0, 5987, -183	/ 57 Ophi
	mark 0, 6006, -292	/ 56 Serp
	mark 0, 6016, -492	/ 58 Ophi
	mark 1, 6047,   63	/ 62 Ophi
	mark 1, 6107, -222	/ 64 Ophi
	mark 0, 6117,  -84	/ 57 Serp
	mark 0, 6117,   99	/ 66 Ophi
	mark 0, 6119,   67	/ 67 Ophi
	mark 0, 6119,  381	/ 93 Herc
	mark 0, 6125,   30	/ 68 Ophi
	mark 0, 6146,   57	/ 70 Ophi
	mark 0, 6158,  198	/ 71 Ophi
	mark 1, 6159,  217	/ 72 Ophi
	mark 0, 6170,  473	/102 Herc
	mark 0, 6188, -480	/ 13 Sgtr
	mark 0, 6234,   76	/ 74 Ophi
	mark 0, 6235,  499	/106 Herc
	mark 1, 6236,  -66	/ 58 Serp
	mark 0, 6247, -204	/ xi Scut
	mark 0, 6254, -469	/ 21 Sgtr
	mark 0, 6255,  494	/109 Herc
	mark 0, 6278, -333	/ ga Scut
	mark 0, 6313, -189	/ al Scut
	mark 0, 6379,  465	/110 Herc
	mark 0, 6382, -110	/ be Scut
	mark 0, 6386,  411	/111 Herc
	mark 0, 6436,   93	/ 63 Serp
	mark 1, 6439, -483	/ 37 Sgtr
	mark 0, 6457,  340	/ 13 Aqil
	mark 0, 6465, -134	/ 12 Aqil
	mark 0, 6478, -498	/ 39 Sgtr
	mark 1, 6490,  312	/ 17 Aqil
	mark 1, 6491, -115	/ 16 Aqil
	mark 1, 6507, -482	/ 41 Sgtr
	mark 0, 6553,  483	/  1 Vulp
	mark 0, 6576, -410	/ 44 Sgtr
	mark 0, 6576, -368	/ 46 Sgtr
	mark 1, 6602,   66	/ 30 Aqil
	mark 0, 6607,    3	/ 32 Aqil
	mark 0, 6651,  163	/ 38 Aqil
	mark 0, 6657,  445	/  9 Vulp
	mark 0, 6665,  -35	/ 41 Aqil
	mark 0, 6688,  405	/  5 Sgte
	mark 0, 6693,  393	/  6 Sgte
	mark 1, 6721,  236	/ 50 Aqil
	mark 0, 6730,  416	/  7 Sgte
	mark 0, 6739,  430	/  8 Sgte
	mark 3, 6747,  196	/ 53 Aqil, Altair
	mark 0, 6755,   17	/ 55 Aqil
	mark 0, 6766,  187	/ 59 Aqil
	mark 0, 6772,  140	/ 60 Aqil
	mark 1, 6794,  437	/ 12 Sgte
	mark 1, 6862,  -25	/ 65 Aqil
	mark 0, 6882,  339	/ 67 Aqil
	mark 0, 6896, -292	/  5 Capr
	mark 0, 6898, -292	/  6 Capr
	mark 0, 6913, -297	/  8 Capr
	mark 1, 6914, -344	/  9 Capr
	mark 0, 6958, -413	/ 11 Capr
	mark 0, 6988,  250	/  2 Dlph
	mark 0, 7001,  326	/  4 Dlph
	mark 1, 7014,  324	/  6 Dlph
	mark 0, 7015,  -33	/ 71 Aqil
	mark 0, 7020,  475	/ 29 Vulp
	mark 0, 7026,  354	/  9 Dlph
	mark 0, 7047,  335	/ 11 Dlph
	mark 0, 7066,  359	/ 12 Dlph
	mark 0, 7067, -225	/  2 Aqar
	mark 0, 7068, -123	/  3 Aqar
	mark 0, 7096, -213	/  6 Aqar
	mark 0, 7161, -461	/ 22 Capr
	mark 0, 7170, -401	/ 23 Capr
	mark 0, 7192, -268	/ 13 Aqar
	mark 0, 7199,  222	/  5 Equl
	mark 0, 7223,  219	/  7 Equl
	mark 0, 7230,  110	/  8 Equl
	mark 0, 7263, -393	/ 32 Capr
	mark 0, 7267,  441	/  1 Pegs
	mark 0, 7299, -506	/ 36 Capr
	mark 1, 7318, -137	/ 22 Aqar
	mark 0, 7347, -453	/ 39 Capr
	mark 0, 7353, -189	/ 23 Aqar
	mark 0, 7365, -390	/ 40 Capr
	mark 0, 7379, -440	/ 43 Capr
	mark 1, 7391,  214	/  8 Pegs
	mark 0, 7394,  384	/  9 Pegs
	mark 1, 7404, -377	/ 49 Capr
	mark 0, 7499,  -60	/ 31 Aqar
	mark 1, 7513,  -18	/ 34 Aqar
	mark 0, 7513,  104	/ 22 Pegs
	mark 0, 7515, -327	/ 33 Aqar
	mark 1, 7539,  130	/ 26 Pegs
	mark 0, 7575, -189	/ 43 Aqar
	mark 0, 7603,  -43	/ 48 Aqar
	mark 0, 7604,  266	/ 31 Pegs
	mark 0, 7624,   20	/ 52 Aqar
	mark 0, 7639,   96	/ 35 Pegs
	mark 1, 7644,  -12	/ 55 Aqar
	mark 0, 7654, -255	/ 57 Aqar
	mark 0, 7681,  -14	/ 62 Aqar
	mark 1, 7717,  235	/ 42 Pegs
	mark 0, 7727, -440	/ 66 Aqar
	mark 0, 7747,  266	/ 46 Pegs
	mark 0, 7761, -321	/ 71 Aqar
	mark 0, 7779, -185	/ 73 Aqar
	mark 1, 7790, -372	/ 76 Aqar
	mark 0, 7795,  189	/ 50 Pegs
	mark 0, 7844,   75	/  4 Pisc
	mark 1, 7849,  334	/ 54 Pegs, Markab
	mark 0, 7862,  202	/ 55 Pegs
	mark 0, 7874, -494	/ 88 Aqar
	mark 0, 7903, -150	/ 90 Aqar
	mark 0, 7911, -219	/ 91 Aqar
	mark 0, 7919,   62	/  6 Pisc
	mark 0, 7923, -222	/ 93 Aqar
	mark 0, 7952, -470	/ 98 Aqar
	mark 0, 7969, -482	/ 99 Aqar
	mark 0, 7975,   16	/  8 Pisc
	mark 0, 7981,  133	/ 10 Pisc
	mark 0, 7988,  278	/ 70 Pegs
	mark 0, 8010, -489	/101 Aqar
	mark 0, 8049,  116	/ 17 Pisc
	mark 0, 8059, -418	/104 Aqar
	mark 0, 8061,   28	/ 18 Pisc
	mark 0, 8064, -344	/105 Aqar
	mark 0, 8159,  144	/ 28 Pisc
	mark 0, 8174, -149	/ 30 Pisc
	mark 0, 8188, -407	/  2 Ceti
ste,
octal


/tables and constants

dimension rt(13)
constants
variables

m1,	/ship outline 1
3	4	4	4	4
4	4	3	3	0
0	0	0	0	0
7	7	0	0	0
0	0	0	0	0
0	7	0	-1

m2,	/ditto ship 2
3	2	2	4	5
5	5	1	3	3
3	0	0	0	0
0	7	1	0	0
7	7	7	0	7
0	-1

m3,	/ditto ship 3
2	4	4	4	6
4	4	4	4	2
0	2	1	2	0
0	0	7	0	0
0	7	7	0	0
0	0	6	0	-1

m4,	/ditto ship 4
3	3	5	6	4
2	0	2	3	4
4	3	3	6	6
1	7	0	0	7
0	1	0	0	0
7	0	0	0	7
7	0	-1


/ship private blocks

/0	skp i	/hyperspace
/1	0	/angular velocity
/2	skp i	/exploding
hub_=3	/8	/hyperspatial uncertainty
/4	0	/unused
/5	-50	/torpedoes left
/6	0	/fuel left
/7	0	/torpedo recharge time left
/10	0	/unused
/11	0	/orientation
/12	0	/collision center offset x
/13	0	/ditto y
/14	skp i	/nonexistent
/15	0	/hyperspace breakout time left
hypr_=16	/0	/hyperspace recharge time left
/17	m1	/shape
/20	b1	/button routine
/21	0	/initial orientation
/22	0	/score
/23	0	/coordinates for score display

l2_=24

define pblock a,c,d
pd'a,	repeat 17,0
	m'a
	jmp b'a
	c
	0
	d
termin

pblock 4,0,0
pblock 3,add,0
pblock 2,jmp,0
pblock 1,lac,600600


/object table

x_=0
y_=1
dx_=2
dy_=3
dxb_=4	/dx low 17 bits
dyb_=5
r_=6
/life_=7
/ships:
/-  explosion life
/0  hyperspace or nonexistent
/1  alive

l_=10

s4,	. 8×l/	/ship object blocks

tor,

start in

‾/
/POINTERS, please

repeat if2,[
	irpc n,1234
		printx /pd'n	/
		printo pd'n
		printc 77
	terminate

	printx /s4	/
	printo s4
	printc 77

	printx /tor	/
	printo tor
	printc 77
]

hd
                              4p*                                             4p*24    4pj